@file:OptIn(ExperimentalWasmDsl::class)

import org.gradle.internal.os.OperatingSystem
import de.undercouch.gradle.tasks.download.Download
import org.gradle.api.internal.file.archive.compression.*
import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.targets.js.binaryen.BinaryenRootEnvSpec
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsExec
import org.jetbrains.kotlin.gradle.targets.wasm.binaryen.BinaryenPlugin
import java.io.*
import java.net.*
import java.util.Locale

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.undercouchDownload) apply false
}

buildscript {
    dependencies {
        // to extract `tar.xz`
        classpath("org.tukaani:xz:1.9")
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

kotlin {
    wasmJs {
        binaries.executable()
        nodejs()
    }

    wasmWasi {
        binaries.executable()
        nodejs()
    }
    
    sourceSets {
        commonMain.dependencies {
            implementation(libs.kotlinx.coroutines.core)
        }
    }
}

apply<BinaryenPlugin>()
the<BinaryenRootEnvSpec>().version.set(libs.versions.binaryen.get())

// Uncomment to turn on stack-switching support
//tasks.withType<org.jetbrains.kotlin.gradle.dsl.KotlinJsCompile>().configureEach {
//    compilerOptions.freeCompilerArgs.addAll(listOf("-Xwasm-use-stack-switching-proposal"))
//}

tasks.withType<org.jetbrains.kotlin.gradle.dsl.KotlinJsCompile>().matching { it.name.contains("wasmWasi", ignoreCase = true) }.configureEach {
    compilerOptions.freeCompilerArgs.addAll(listOf("-Xwasm-use-new-exception-proposal=false"))
}

enum class OsName { WINDOWS, MAC, LINUX, UNKNOWN }
enum class OsArch { X86_32, X86_64, ARM64, UNKNOWN }
data class OsType(val name: OsName, val arch: OsArch)

val currentOsType = run {
    val gradleOs = OperatingSystem.current()
    val osName = when {
        gradleOs.isMacOsX -> OsName.MAC
        gradleOs.isWindows -> OsName.WINDOWS
        gradleOs.isLinux -> OsName.LINUX
        else -> OsName.UNKNOWN
    }

    val arch = providers.systemProperty("os.arch").get().lowercase(Locale.getDefault())
    val osArch = when {
        arch == "x86_64" || arch == "amd64" -> OsArch.X86_64
        arch == "aarch64" || arch == "arm64" -> OsArch.ARM64
        arch.contains("86") -> OsArch.X86_32
        else -> OsArch.UNKNOWN
    }

    OsType(osName, osArch)
}

// Wasmtime tasks
val wasmtimeVersion = libs.versions.wasmtime.get()

val wasmtimeSuffix = when (currentOsType) {
    OsType(OsName.LINUX, OsArch.X86_64)   -> "x86_64-linux"
    OsType(OsName.LINUX, OsArch.ARM64)    -> "aarch64-linux"
    OsType(OsName.MAC, OsArch.X86_64)     -> "x86_64-macos"
    OsType(OsName.MAC, OsArch.ARM64)      -> "aarch64-macos"
    OsType(OsName.WINDOWS, OsArch.X86_32),
    OsType(OsName.WINDOWS, OsArch.X86_64) -> "x86_64-windows"

    else                                  -> error("unsupported os type $currentOsType")
}

val wasmtimeArtifactName = "wasmtime-$wasmtimeVersion-$wasmtimeSuffix"

val unzipWasmtime = run {
    val wasmtimeDirectory = "https://github.com/bytecodealliance/wasmtime/releases/download/$wasmtimeVersion"
    val archiveType = if (currentOsType.name == OsName.WINDOWS) "zip" else "tar.xz"
    val wasmtimeArchiveName = "$wasmtimeArtifactName.$archiveType"
    val wasmtimeLocation = "$wasmtimeDirectory/$wasmtimeArchiveName"

    val downloadedTools = layout.buildDirectory.dir("tools")

    val downloadWasmtime = tasks.register("wasmtimeDownload", Download::class) {
        src(wasmtimeLocation)
        dest(downloadedTools.map { it.file(wasmtimeArchiveName) })
        overwrite(false)
    }

    tasks.register("wasmtimeUnzip", Copy::class) {
        dependsOn(downloadWasmtime)

        val archive = downloadWasmtime.map { it.dest }

        from(archive.map { if (it.extension == "zip") zipTree(it) else tarTree(XzArchiver(it)) })

        into(downloadedTools)
    }
}

private class XzArchiver(private val file: File) : CompressedReadableResource {
    override fun read(): InputStream = org.tukaani.xz.XZInputStream(file.inputStream().buffered())
    override fun getURI(): URI = URIBuilder(file.toURI()).schemePrefix("xz:").build()
    override fun getBackingFile(): File = file
    override fun getBaseName(): String = file.name
    override fun getDisplayName(): String = file.path
}

fun Project.createWasmtimeExec(
    nodeMjsFile: RegularFileProperty,
    taskName: String,
    taskGroup: String?,
    startFunction: String
): TaskProvider<Exec> {
    return tasks.register(taskName, Exec::class) {
        dependsOn(unzipWasmtime)

        taskGroup?.let { group = it }

        description = "Executes tests with Wasmtime"

        val wasmtimeDirectory = unzipWasmtime.map { it.destinationDir.resolve(wasmtimeArtifactName) }

        doFirst {
            val executableName = if (currentOsType.name == OsName.WINDOWS) "wasmtime.exe" else "wasmtime"
            executable(wasmtimeDirectory.get().resolve(executableName).absolutePath)

            val file = nodeMjsFile.get().asFile
            val outputDirectory = file.parentFile
            val wasmFileName = file.nameWithoutExtension + ".wasm"

            workingDir(outputDirectory)

            args(
                "-W", "function-references,gc,exceptions,stack-switching",
                "-D", "logging=y",
                "--invoke", startFunction,
                wasmFileName
            )
        }

        // to show stacktraces
        environment("RUST_BACKTRACE", "full")
    }
}


tasks.withType<NodeJsExec>().matching { it.name.contains("wasmWasi", ignoreCase = true) }.all {
    val wasmtimeRunTask = createWasmtimeExec(
        inputFileProperty,
        name.replace("Node", "Wasmtime"),
        group,
        "_start"
    )

    wasmtimeRunTask.configure {
        dependsOn(
            project.provider { this@all.taskDependencies }
        )
    }
}